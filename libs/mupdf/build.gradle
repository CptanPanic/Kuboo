apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'

group = 'com.artifex.mupdf'
version = '1.13.0'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    if (file('../jni/build.gradle').isFile())
        implementation project(':jni')
    else
        implementation 'com.artifex.mupdf:fitz:1.13.0'

    final android_support_version = "27.1.1"
    implementation "com.android.support:appcompat-v7:$android_support_version"

    final android_lifecycle_version = "1.1.1"
    implementation "android.arch.lifecycle:extensions:$android_lifecycle_version"
    implementation "android.arch.lifecycle:runtime:$android_lifecycle_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'file://MAVEN')
            pom {
                artifactId = 'mini'
                project {
                    licenses {
                        license {
                            name 'GNU Affero General Public License'
                            url 'https://www.gnu.org/licenses/agpl-3.0.html'
                        }
                    }
                }
            }
        }
    }
}
repositories {
    mavenCentral()
}
